apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "com.google.protobuf"

repositories {
    mavenCentral()
}

dependencies {
    api project(':commons-java')

    implementation rootProject.ext.kotlinLibs.values()
    implementation rootProject.ext.kotlinAndroidLibs.values()
    implementation rootProject.ext.supportLibs.core
    implementation rootProject.ext.gpsLibs.basement // update security provider for SSL
    implementation rootProject.ext.protobufLibs.javalite
    implementation rootProject.ext.retrofitLibs.values()
    implementation rootProject.ext.okHttpLibs.values()
    implementation rootProject.ext.gsonLibs.values()

    testImplementation rootProject.ext.jUnitLibs.jUnit
    testImplementation rootProject.ext.mockitoLibs.mockitoCore
}

protobuf {
    protoc {
        artifact = rootProject.ext.protobufLibs.compiler
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.javaVersion
        targetCompatibility rootProject.javaVersion
    }
    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinOptionsJvmTarget
    }

    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        manifestPlaceholders = [
                permission_provider_read     : "org.mtransit.android.provider.permission.READ_PROVIDER",
                permission_receiver_broadcast: "org.mtransit.android.receiver.permission.BROADCAST_RECEIVER"
        ]
    }

    buildTypes {
        debug {
            manifestPlaceholders = [
                    permission_provider_read     : "org.mtransit.android.debug.provider.permission.READ_PROVIDER",
                    permission_receiver_broadcast: "org.mtransit.android.debug.receiver.permission.BROADCAST_RECEIVER"
            ]
        }
    }

    lintOptions {
        abortOnError true
        // abortOnError false // DEBUG
        ignoreWarnings false
        // ignoreWarnings true // DEBUG
        quiet false
        // quiet  true // DEBUG
        warning 'InvalidPackage'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
                testLogging {
                    events 'passed', 'skipped', 'failed'
                }
            }
        }
    }
}
